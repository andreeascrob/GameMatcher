------------------------------------------------------

QUERY CQ1:
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?nameGame 
WHERE {
  # Define the input
  VALUES (?targetUser) { 
    ("Giulio") 
  }

  # Define specific user
  ?user rdfs:label ?name .
  FILTER(CONTAINS(LCASE(?name), LCASE(?targetUser)))

  # All videogames in the database
  ?videogame rdfs:label ?nameGame .

  # User PC LOGIC
  ?user onto:hasComputer ?pc .
  ?pc onto:hasCPU ?cpuU ;
      onto:hasGPU ?gpuU ;
      onto:hasRAM ?ramU ;
      onto:hasOS ?osU ;
      onto:hasStorage ?romU.
      
  ?cpuU onto:hasBenchmarkScore ?cpu_scoreU .
  ?gpuU onto:hasBenchmarkScore ?gpu_scoreU .
  ?ramU onto:hasMemorySizeGB ?ram_sizeU .
  ?romU onto:hasStorageSizeGB ?rom_sizeU .
  ?osU onto:hasOSVersionValue ?os_versionU .
  

  # Requirement Logic
  ?videogame onto:hasMinRequirement ?min_req .
  ?videogame onto:hasPrice ?price .

  ?min_req onto:requiresCPUBenchmarkScore ?cpu_scoreG ;
           onto:requiresGPUBenchmarkScore ?gpu_scoreG ;
           onto:requiresMemoryGB ?ram_sizeG ;
           onto:requiresStorageSpaceGB ?rom_sizeG ;
           onto:requiresMinOSVersionValue ?os_versionG .
            

  # Comparations for testing
  BIND(IF(?cpu_scoreU >= ?cpu_scoreG, "OK", "WEAK") AS ?cpu_status)
  BIND(IF(?gpu_scoreU >= ?gpu_scoreG, "OK", "WEAK") AS ?gpu_status)
  BIND(IF(?ram_sizeU >= ?ram_sizeG, "OK", "WEAK") AS ?ram_status)
  BIND(IF(?rom_sizeU >= ?rom_sizeG, "OK", "WEAK") AS ?storage_status)
  BIND(IF(?os_versionU >= ?os_versionG, "OK", "WEAK") AS ?os_status)

  # Filtering
  FILTER(?cpu_scoreU >= ?cpu_scoreG && ?gpu_scoreU >= ?gpu_scoreG && ?ram_sizeU >= ?ram_sizeG && ?rom_sizeU >= ?rom_sizeG && ?os_versionU >= ?os_versionG)
}

------------------------------------------------------

Query CQ2: 
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?nameGame ?price
WHERE {
  # Define the input
  VALUES (?targetUser) { 
    ("Giulio") 
  }

  # Define specific user
  ?user rdfs:label ?name .
  FILTER(CONTAINS(LCASE(?name), LCASE(?targetUser)))

  # All videogames in the database
  ?videogame rdfs:label ?nameGame .

  # User Logic
  ?user onto:hasBudget ?budget .
  
  # Game Logic
  ?videogame onto:hasPrice ?price .
            
  # Comparation for testing
  BIND(IF(?price <= ?budget, "CanBuy", "TooExpensive") AS ?budget_status)
 
  # Budget Filter
  FILTER(?price <= ?budget)
}
ORDER BY ASC(?price)

------------------------------------------------------

Query CQ3: 
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?nameGame ?pegi_value
WHERE {
  # Define the input
  VALUES (?targetUser) { 
    ("Giulio") 
  }

  # Define specific user
  ?user rdfs:label ?name .
  FILTER(CONTAINS(LCASE(?name), LCASE(?targetUser)))

  # All videogames in the database
  ?videogame rdfs:label ?nameGame .

  # User Logic
  ?user onto:hasAge ?age .
  
  # Game Logic
  ?videogame onto:hasPEGI ?pegi .

  ?pegi onto:hasPEGIAgeThreshold ?pegi_value.
            
  # Comparation for testing
  BIND(IF(?pegi_value <= ?age, "CanPlay", "TooYoung") AS ?pegi_status)
 
  # Budget Filter
  FILTER(?pegi_value <= ?age)
}

------------------------------------------------------

Query CQ4:
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# We use DISTINCT to ensure that if a game falls into multiple 
# preferred genres (e.g., Action-RPG), it is not listed twice.
SELECT DISTINCT ?nameGame ?genreLabel
WHERE {
  # 1. INPUT: Define the target user
  VALUES (?targetUser) { 
    ("Dev_User") 
  }

  # 2. MATCH USER: Find the User individual by name
  ?user rdfs:label ?name .
  FILTER(CONTAINS(LCASE(?name), LCASE(?targetUser)))

  # 3. GET PREFERENCES: Retrieve ALL genres preferred by the user.
  # If Giulio prefers 3 genres, this variable ?genre will hold 
  # 3 different values sequentially.
  ?user onto:preferredGenre ?genre .
  ?genre rdfs:label ?genreLabel .

  # 4. MATCH GAMES: Find games linked to those specific genre nodes.
  ?videogame a onto:VideoGame ;
             rdfs:label ?nameGame ;
             onto:hasGenre ?genre .
}
ORDER BY ?genreLabel

---------------------------------------------------------------------------------------------------
Query CQ5:
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?nameGame ?modeLabel
WHERE {
  # Define the input user
  VALUES (?targetUser) { 
    ("Mark") 
  }

  # Find the specific user
  ?user rdfs:label ?name .
  FILTER(CONTAINS(LCASE(?name), LCASE(?targetUser)))

  # 1. Get User's Preferred Player Modes
  ?user onto:preferredPlayerMode ?mode .
  ?mode rdfs:label ?modeLabel .

  # 2. Find Games that support THAT mode
  ?videogame a onto:VideoGame ;
             rdfs:label ?nameGame ;
             onto:hasPlayerMode ?mode .
}
ORDER BY ?nameGame

---------------------------------------------------------------------------------------------------------------
Query CQ6:
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?nameGame ?requiredRAM
WHERE {
  # Define the input threshold Z (e.g., games requiring MORE than 4 GB)
  BIND(10.0 AS ?thresholdRAM)

  # Get all videogames
  ?videogame a onto:VideoGame ;
             rdfs:label ?nameGame .

  # Navigate to requirements
  ?videogame onto:hasMinRequirement ?min_req .
  
  # Get the numeric RAM requirement value
  ?min_req onto:requiresMemoryGB ?requiredRAM .

  # Filter logic: Keep games where required RAM > threshold
  FILTER(?requiredRAM > ?thresholdRAM)
}
ORDER BY DESC(?requiredRAM)

------------------------------------------------------

QUERY N1:
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?nameGame
WHERE {
  VALUES ?targetUser { "Giulio" }
  ?user rdfs:label ?userName .
  FILTER(CONTAINS(LCASE(?userName), LCASE(?targetUser)))

  ?videogame onto:isPerfectMatchFor ?user .
  
  ?videogame rdfs:label ?nameGame .
}
------------------------------------------------------

QUERY N2:
-

PREFIX onto: <http://www.semanticweb.org/gamematcher.owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?nameGame 
WHERE {
  VALUES ?targetUser { "Marco" }
  ?user rdfs:label ?userName .
  FILTER(CONTAINS(LCASE(?userName), LCASE(?targetUser)))
  
  ?user onto:hasComputer ?pc . 

  ?videogame rdfs:label ?nameGame .
  ?videogame onto:hasMinRequirement ?min_req .

  ?min_req onto:isPerfectHardwareMatch ?pc .

}